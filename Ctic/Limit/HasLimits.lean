import Ctic.Limit.Basic
import Ctic.Limit.Diagram
import Ctic.Repre.Contravariant
import Ctic.Repre.Notation

namespace CTIC

abbrev HasLimit {J C : Type*} [Category J] [Category C] (F : J ‚•§ C) : Prop := Nonempty (Limit F)

class HasLimitsOfShape (C J : Type*) [Category C] [Category J] : Prop where
  limits : ‚àÄ (F : J ‚•§ C), HasLimit F

class HasFiniteProducts (C : Type u) [Category C] : Prop where
  proj : ‚àÄ (n : ‚Ñï), HasLimitsOfShape C (Discrete (Fin n))

open Diagram in
abbrev HasTerminal (C : Type*) [Category C] : Prop := HasLimitsOfShape C ùü¨

example : HasTerminal Type where
  limits F := by
    apply Nonempty.intro
    let c : Cone F := ‚ü®Unit, ‚ü®(fun x y => by exfalso; apply Diagram.Nullary.empty x), by intro X Y f; exfalso; apply Diagram.Nullary.empty X‚ü©‚ü©
    use c
    apply Terminal.mk
    case morphism =>
      intro ‚ü®L, œÄ‚ü©
      simp [c]
      simp [Category.Hom]
      use fun _ => ()
      intro j
      exfalso
      apply Diagram.Nullary.empty j
    case unique_morphism =>
      intro L f
      simp
      congr

open Diagram in
noncomputable def HasTerminal.terminal {C : Type*} [Category C] [has_terminal : HasTerminal C] : C := by
  let F : ùü¨ ‚•§ C := by
    refine ‚ü®(fun x => by exfalso; apply Nullary.empty x), fun f => by simp; exact ùüô _, ?_, ?_‚ü©
    . intro X
      simp
    . intro X Y Z f g
      simp
  have has_limit := has_terminal.limits F
  let t := has_limit.some
  exact t.L.N

class HasFiniteLimitsOf.{u, v, u_1, u_2} (C : Type u) [Category.{u_1, u} C] (n : ‚Ñï) : Prop where
  proj : ‚àÄ (J : Type v) [Category J], (Fin n ‚âÉ J) ‚Üí HasLimitsOfShape.{u, v, u_1, u_2} C J

class HasFiniteLimits.{u, v, u_1, u_2} (C : Type u) [Category.{u_1, u} C] : Prop where
  proj : ‚àÄ (n : ‚Ñï), HasFiniteLimitsOf.{u, v, u_1, u_2} C n

inductive Centered (Œ± : Type u) where
  | peri (a : Œ±)
  | center

instance [Category Œ±] : Category (Centered Œ±) where
  Hom X Y := by
    match X, Y with
    | .peri x, .peri y => exact Category.Hom x y
    | .peri x, .center => exact PUnit
    | .center, .center => exact PUnit
    | .center, .peri _ => exact PEmpty
  id X := by
    match X with
    | .peri X => exact ùüô X
    | .center => exact PUnit.unit
  comp {X Y Z} f g := by
    match X, Y, Z with
    | .peri X, .peri Y, .peri Z => simp at f g; simp; exact f ‚â´ g
    | .peri X, _, .center => simp; exact PUnit.unit
    | .center, .center, .center => simp; exact PUnit.unit
  id_comp {X Y} f := by
    match X, Y with
    | .peri x, .peri y => simp
    | .peri x, .center => simp
    | .center, .center => simp
  comp_id {X Y} f := by
    match X, Y with
    | .peri x, .peri y => simp
    | .peri x, .center => simp
    | .center, .center => simp
  assoc {W X Y Z} f g h := by
    match W, X, Y, Z with
    | .peri W, .peri X, .peri Y, .peri Z => simp
    | .peri W, .peri X, _, .center => simp
    | .peri W, .center, .center, .center => simp
    | .center, .center, .center, .center => simp

def Centered.add_equiv : (Fin n ‚âÉ Œ±) ‚Üí (Fin (n + 1) ‚âÉ Centered Œ±) := by
  intro equiv
  let f : Fin (n + 1) ‚Üí Centered Œ± := fun x =>
    if h : x = n then .center
    else .peri (equiv.toFun ‚ü®x.val, by have := Nat.le_of_lt_add_one x.isLt; apply Nat.lt_of_le_of_ne this h‚ü©)
  let g : Centered Œ± ‚Üí Fin (n + 1) := fun x =>
    match x with
    | .peri x => ‚ü®(equiv.invFun x).val, by have := (equiv.invFun x).isLt; apply Nat.lt_add_right _ this‚ü©
    | .center => ‚ü®n, by simp‚ü©
  refine ‚ü®f, g, ?_, ?_‚ü©
  . intro x
    simp [g, f]
    if h : x = n then
      rw [dif_pos h]
      simp
      congr
      simp [h]
    else
      rw [dif_neg h]
      simp
  . intro x
    dsimp [f]
    split
    . rename_i h
      simp [g] at h
      split at h
      . simp at h
        rename_i a
        have : equiv.symm a ‚â• n := by simp [h]
        have : equiv.symm a < n := by simp
        aesop
      . rfl
    . rename_i h
      simp [g] at h
      cases x with
      | center => simp at h
      | peri x' => simp [g]

example (C : Type u) [SmallCategory.{u} C] [has_finite_limits : HasFiniteLimitsOf.{u, v, u, u'} C (n + 1)] (X : C) : HasFiniteLimitsOf.{u, v, u, u'} (SliceOver X) n := by
  constructor
  intro J _ equiv
  let equiv' := Centered.add_equiv equiv
  have ‚ü®h‚ü© := has_finite_limits.proj (Centered J) equiv'
  constructor
  intro F
  let G : Centered J ‚•§ C := by
    -- constructor
    let u : Centered J ‚Üí C := fun x =>
      match x with
      | .center => X
      | .peri x => (F.obj x).var
    use u
    . intro x y f
      simp [u]
      match x, y with
      | .center, .center => exact ùüô X
      | .peri x, .peri y =>
        simp
        change x ‚ü∂ y at f
        exact (F.map f).val
      | .peri x, .center =>
        simp
        exact (F.obj x).hom
    . simp
      intro x
      cases x with
      | center => simp [u]
      | peri x' =>
        simp [u]
        -- rw [F.map_id (X := x')]
        let g : x' ‚ü∂ x' := ùüô Centered.peri x'
        change (F.map g).val = ùüô (F x').var
        have : g = ùüô x' := by
          rfl
        rw [this]
        rw [F.map_id]
        congr
    . intro x y z f g
      simp
      match x, y, z with
      | .peri x, .peri y, .peri z =>
        simp
        change x ‚ü∂ y at f
        change y ‚ü∂ z at g
        change (F.map (f ‚â´ g)).val = (F.map f).val ‚â´ (F.map g).val
        rw [F.map_comp]
        congr
      | .peri x, .peri y, .center =>
        change x ‚ü∂ y at f
        simp
        rw [(F.map f).prop]
      | .peri x, .center, .center =>
        simp
        rw [Category.comp_id]
      | .center, .center, .center =>
        simp
        rw [Category.id_comp]
  have forward : ‚àÄ L : Limit F, ‚àÉ L' : Limit G, L.L.N.var = L'.L.N := by
    intro ‚ü®‚ü®‚ü®L, p‚ü©, œÄ‚ü©, h‚ü©
    simp
    let cone : Cone G := by
      use L
      constructor
      case component =>
        intro x
        simp
        match x with
        | .peri x' => exact œÄ.component x' |>.val
        | .center => exact p
      case naturality =>
        intro x y f
        simp
        match x, y with
        | .peri x', .peri y' =>
          simp
          change (œÄ.component x').val ‚â´ G.map f = ùüô L ‚â´ (œÄ.component y').val
          rw [Category.id_comp]
          have := œÄ.naturality f
          simp at this
          change œÄ.component x' ‚â´ F.map f = ùüô { var := L, hom := p : SliceOver X } ‚â´ œÄ.component y' at this
          rw [Category.id_comp] at this
          rw [‚Üê this]
          congr
        | .peri x', .center =>
          simp
          change (œÄ.component x').val ‚â´ G.map f = ùüô L ‚â´ p
          rw [Category.id_comp]
          simp [G]
          rw [(œÄ.component x').prop]
          congr
        | .center, .center =>
          simp [G]
          change p = ùüô L ‚â´ p
          simp
    let L' : Limit G := by
      use cone
      constructor
      case morphism =>
        intro c
        let c' : Cone F := by
          let s : SliceOver X := by
            use c.N
            exact c.œÄ'.component .center
          use s
          constructor
          case component =>
            intro x
            simp
            use c.œÄ'.component (.peri x)
            simp [s]
            have := c.œÄ'.naturality (X := .peri x) (Y := .center) (PUnit.unit)
            simp [G] at this
            rw [this]
            change ùüô c.N ‚â´ _ = _
            simp
          case naturality =>
            intro x y f
            simp
            change _ ‚â´ _ = ùüô s ‚â´ _
            simp
            simp [Category.comp]
            congr
            change Centered.peri x ‚ü∂ Centered.peri y at f
            have := c.œÄ'.naturality f
            simp [G] at this
            rw [this]
            change ùüô _ ‚â´ _ = _
            simp
        constructor
        case u => exact h.morphism c' |>.u.val
        case universal =>
          intro x
          simp [cone]
          match x with
          | .center =>
            simp
            rw [h.morphism c' |>.u.prop]
          | .peri x' =>
            simp
            have := h.morphism c' |>.universal x'
            simp at this
            simp [Category.comp] at this
            conv at this => rhs; simp [c']
            rw [Subtype.eq_iff] at this
            simp at this
            exact this
      case unique_morphism =>
        intro c ‚ü®f, h'‚ü©
        congr
        let s : SliceOver X := { var := c.N, hom := c.œÄ'.component Centered.center }
        let component : (x : J) ‚Üí (Œî s) x ‚ü∂ F x := by
          intro x
          simp
          let t := c.œÄ'.component (Centered.peri x)
          use t
          simp [t]
          have := c.œÄ'.naturality (X := .peri x) (Y := .center) (PUnit.unit)
          simp [G] at this
          rw [this]
          change ùüô _ ‚â´ _ = _
          simp
          rfl
        have naturality : ‚àÄ {X Y : J} (f : X ‚ü∂ Y), component X ‚â´ F.map f = (Œî s).map f ‚â´ component Y := by
          intro x y f
          simp
          change _ = ùüô _ ‚â´ _
          simp [component]
          simp [Category.comp]
          congr
          change Centered.peri x ‚ü∂ Centered.peri y at f
          have := c.œÄ'.naturality f
          simp [G] at this
          rw [this]
          change ùüô _ ‚â´ _ = _
          simp
        let c' : Cone F := { N := s, œÄ' := { component, naturality } }
        change f = (h.morphism c').u.val
        -- have := h.unique_morphism
        let f' : c' ‚ü∂ { N := { var := L, hom := p }, œÄ' := œÄ } := by
          constructor
          case u =>
            simp [c', s]
            constructor
            case val => use f
            simp
            have := h' .center
            simp [cone] at this
            exact this
          intro x
          simp [Category.comp, c', component]
          congr
          have := h' (Centered.peri x)
          simp [cone] at this
          exact this
        have := h.unique_morphism f'
        change f'.u.val = _
        congr
    use L'
